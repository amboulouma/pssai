% Number of shift types
int: numShifts;
% Number of employees
int: groups;
% number of days
int: m=7; 

int: minOn; % lower bound for working day
int: maxOn; % upper bound for working day

int: minOff; % lower bound for dayoff
int: maxOff; % upper bound for dayoff

array[int] of int: minShift;
array[int] of int: maxShift;

int: d_min=minShift[1]; % lower bound for dayshift
int: d_max=maxShift[1]; % upper bound for dayshift
int: a_min=minShift[2]; % lower bound for afternoon shift
int: a_max=maxShift[2]; % upper bound for afternoon shift
int: n_min=minShift[3]; % lower bound for nightshift
int: n_max=maxShift[3]; % upper bound for nightshift

set of int: EMPLOYEE = 1..groups;
set of int: DAY = 1..m;

set of int: SHIFT = 1..numShifts+1;
int: day = 1; int: afternoon = 2; int: night = 3; int: dayoff = 4;
array[SHIFT] of string: code = ["D", "A", "N", "-"];

array[EMPLOYEE,DAY] of var SHIFT: x;

% constraint forall(e in EMPLOYEE, d in 1..m-2)
%                 ( x[e,d] = night /\ x[e,d+1] = night 
%                   -> x[e,d+2] = dayoff);
                   
% Forbidden (if forbidden)
%% Forbidden afternoon
constraint forall(e in EMPLOYEE, d in 1..m-1)
                 (x[e,d] = afternoon -> x[e,d+1] != day);

%% Forbidden nights
constraint forall(e in EMPLOYEE, d in 1..m-1)
                 (x[e,d] = night -> x[e,d+1] != day);

constraint forall(e in EMPLOYEE, d in 1..m-1)
                 (x[e,d] = night -> x[e,d+1] != afternoon);

% Forbidden 3 (if forbidden3)
constraint forall(e in EMPLOYEE, d in 1..m-2)
                 (x[e,d] = night /\ x[e,d+1] = dayoff -> x[e,d+2] != night);
constraint forall(e in EMPLOYEE, d in 1..m-2)
                 (x[e,d] = night /\ x[e,d+1] = dayoff -> x[e,d+2] != day);
constraint forall(e in EMPLOYEE, d in 1..m-2)
                 (x[e,d] = night /\ x[e,d+1] = dayoff -> x[e,d+2] != afternoon);
constraint forall(e in EMPLOYEE, d in 1..m-2)
                 (x[e,d] = afternoon /\ x[e,d+1] = dayoff -> x[e,d+2] != day);                 


include "global_cardinality_low_up.mzn";
constraint forall(e in EMPLOYEE)
                 (global_cardinality_low_up([x[e,d] | d in DAY ],
                   [ day, afternoon, night, dayoff ], 
                   [ d_min, a_min, n_min, minOff ], 
                   [ d_max, a_max, n_max, maxOff ])); 

solve satisfy;

output [ code[fix(x[e,d])] 
         ++ if d = m then "\n" else " " endif
       | e in EMPLOYEE, d in DAY];
